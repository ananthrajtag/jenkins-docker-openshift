println "Load repositories.gradle init scripts..."

allprojects {
    if (System.env.MAVEN_MIRROR_URL?.trim()) {
        buildscript {
            repositories {
                mavenLocal()
                maven { url System.env.MAVEN_MIRROR_URL }
            }
        }
        repositories {
            mavenLocal()
            maven { url System.env.MAVEN_MIRROR_URL }
        }
    }
    task showPublishRepositories {
        doLast {
            if (project.hasProperty('publishing')) {
                println "List of repositories to publish artifacts:"
                project.publishing.repositories.forEach {
                    println "${it.name}: ${it.url}"
                }
            }
        }
    }
}

projectsEvaluated {
    allprojects {
        pluginManager.withPlugin('maven-publish') {
            if (System.env.MAVEN_PUBLISH_URL?.trim()) {
                publishing.repositories {
                    // clear all repo
                    clear()
                    /**
                     * Workaround since gradle 4.7, it creates a task for each repo that you add.
                     * So we need to remove the old tasks `publishMavenJavaPublicationToMavenRepository` before adding a new maven repo, 
                     * otherwise the build will fail complaining that the task publishMavenJavaPublicationToMavenRepository already exists.
                     */
                    if (project.gradle.gradleVersion >= '4.7' && !project.getTasksByName('publishMavenJavaPublicationToMavenRepository', false).isEmpty()) {
                        project.tasks.remove(project.publishMavenJavaPublicationToMavenRepository)
                    }
                    // add the new one
                    maven {
                        if (System.env.MAVEN_PUBLISH_SNAPSHOT_URL?.trim() && project.version.endsWith('-SNAPSHOT')) {
                            url = System.env.MAVEN_PUBLISH_SNAPSHOT_URL
                        } else {
                            url = System.env.MAVEN_PUBLISH_URL
                        }
                        if (System.env.MAVEN_PUBLISH_USERNAME?.trim() &&
                            System.env.MAVEN_PUBLISH_PASSWORD?.trim()) {
                            credentials {
                                username = System.env.MAVEN_PUBLISH_USERNAME
                                password = System.env.MAVEN_PUBLISH_PASSWORD
                            }
                        }
                    }
                }
            }
        }
    }
}
